plugins {
    id 'groovy'
    id 'maven-publish'
    id 'org.ajoberstar.bintray' version '0.1.0'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

model {
    bintray {
        owner = 'ajoberstar'
        repo = 'maven'
        pkg = 'reckon'
    }
}

stutter {
    supports '3.0', '3.1', '3.2', '3.2.1'
}

group = 'org.ajoberstar'

configurations.all {
    exclude group: 'org.codehaus.groovy'
}

sourceCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile project(':reckon-core')
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    compatTestCompile gradleTestKit()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.outputs.files
}

publishing {
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/reckon'
    vcsUrl = 'https://github.com/ajoberstar/reckon.git'
    description = 'Infer a project\'s version from your Git repository'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.reckon'
            displayName = 'Reckon Plugin'
            tags = ['semver', 'git']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}
