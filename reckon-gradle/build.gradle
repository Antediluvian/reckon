plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

apply from: '../gradle/compatTest.gradle'

group = 'org.ajoberstar'

configurations.all {
    exclude group: 'org.codehaus.groovy'
}

sourceCompatibility = '1.8'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile project(':reckon-core')
    // runtime 'org.ajoberstar:gradle-git:1.3.0'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    compatTestCompile gradleTestKit()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.outputs.files
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = 'bintray'
            url = 'https://api.bintray.com/maven/ajoberstar/maven/reckon/;publish=1'
            credentials {
                username = System.env['BINTRAY_USER']
                password = System.env['BINTRAY_KEY']
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/reckon'
    vcsUrl = 'https://github.com/ajoberstar/reckon.git'
    description = 'Infer a project\'s version from your Git repository'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.reckon'
            displayName = 'Reckon Plugin'
            tags = ['semver', 'git']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}
