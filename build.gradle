buildscript {
    repositories { jcenter() }
    dependencies { classpath 'org.ajoberstar:gradle-defaults:0.6.0-milestone.2' }
}

apply plugin: 'org.ajoberstar.defaults'
apply plugin: 'com.jfrog.bintray'

ext.depVersions = [
    groovy: '2.4.1',
    spock: '1.0-groovy-2.4'
]

defaults {
    id = 'ajoberstar'

    bintrayRepo = 'libraries'
    bintrayLabels = ['vcs', 'git', 'release', 'semver']

    developers = [
        [id: 'ajoberstar', name: 'Andrew Oberstar', email: 'andrew@ajoberstar.org']
    ]

    copyrightYears = '2015'
}

allprojects {
    apply plugin: 'maven-publish'
    group = 'org.ajoberstar'
}

subprojects {
    sourceCompatibility = '1.8'

    repositories {
        jcenter()
    }
}

project(':semver-vcs-api') {
    apply plugin: 'groovy'

    dependencies {
        compile 'com.github.zafarkhaja:java-semver:0.9.0'
        testCompile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        testCompile "org.spockframework:spock-core:${depVersions.spock}"
    }
}

project(':semver-vcs-test') {
    apply plugin: 'groovy'

    dependencies {
        compile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        compile "org.spockframework:spock-core:${depVersions.spock}"
        compile project(':semver-vcs-api')
    }
}

project(':semver-vcs-grgit') {
    apply plugin: 'groovy'

    dependencies {
        compile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        compile project(':semver-vcs-api')
        compile 'org.ajoberstar:grgit:1.2.0'
        testCompile project(':semver-vcs-test')
    }
}
