buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.2.0-milestone.5'
        classpath 'org.ajoberstar:gradle-defaults:0.6.0-milestone.7'
    }
}

apply plugin: 'org.ajoberstar.defaults'

ext.depVersions = [
    groovy: '2.4.1',
    spock: '1.0-groovy-2.4'
]

defaults {
    id = 'ajoberstar'

    bintrayRepo = 'libraries'
    bintrayLabels = ['vcs', 'git', 'semver', 'gradle']
    bintrayToCentral = false

    developers = [
        [id: 'ajoberstar', name: 'Andrew Oberstar', email: 'andrew@ajoberstar.org']
    ]

    copyrightYears = '2015'
}

allprojects {
    group = 'org.ajoberstar'
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = '1.8'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        testCompile "org.spockframework:spock-core:${depVersions.spock}"
    }
}

project(':semver-vcs-api') {
    dependencies {
        compile 'com.github.zafarkhaja:java-semver:0.9.0'
        testCompile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        testCompile "org.spockframework:spock-core:${depVersions.spock}"
    }
}

project(':semver-vcs-test') {
    dependencies {
        compile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        compile "org.spockframework:spock-core:${depVersions.spock}"
        compile project(':semver-vcs-api')
    }
}

project(':semver-vcs-grgit') {
    dependencies {
        compile "org.codehaus.groovy:groovy-all:${depVersions.groovy}"
        compile project(':semver-vcs-api')
        compile 'org.ajoberstar:grgit:1.2.0'
        testCompile project(':semver-vcs-test')
    }
}

project(':semver-vcs-gradle-base') {
    configurations.all {
        exclude group: 'org.codehaus.groovy'
    }

    dependencies {
        compile gradleApi()
        compile project(':semver-vcs-api')
        testCompile "org.spockframework:spock-core:${depVersions.spock}"
    }
}

project(':semver-vcs-gradle-grgit') {
    configurations.all {
        exclude group: 'org.codehaus.groovy'
    }

    dependencies {
        compile gradleApi()
        compile project(':semver-vcs-gradle-base')
        compile project(':semver-vcs-grgit')
        runtime 'org.ajoberstar:gradle-git:1.2.0-milestone.4'
        testCompile "org.spockframework:spock-core:${depVersions.spock}"
    }
}
